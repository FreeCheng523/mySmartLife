plugins {
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}

android {
    namespace 'com.deepal.ivi.hmi.smartlife'
    compileSdk 34

    defaultConfig {
        applicationId "com.deepal.ivi.hmi.smartlife"
        minSdk 30
        targetSdk 34
        versionCode 20000
        versionName "2.00.00"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }
    //签名配置
    signingConfigs {
        debug {
            keyAlias 'platform'
            keyPassword 'android'
            storeFile file('../aosp.keystore')
            storePassword 'android'
        }
        release {
            keyAlias 'platform'
            keyPassword 'android'
            storeFile file('../aosp.keystore')
            storePassword 'android'
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    def carPlatformType = project.findProperty("CAR_PLATFORM_TYPE") ?: "none"
    def includeInstrumentPanel = project.findProperty("INCLUDE_INSTRUMENT_PANEL") ?: "false"
    def currentVehicleType = rootProject.ext.has("currentVehicleType") ? rootProject.ext.currentVehicleType : "none"
    def enable_fragrance = rootProject.ext.has("enable_fragrance") ? rootProject.ext.enable_fragrance : "none"


    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def date = new Date().format('yyyyMMdd')
                def versionName = variant.versionName
                outputFileName = "DeepalSmartLife_v${versionName.replace('.', '_')}_${date}_${currentVehicleType}_ip${includeInstrumentPanel}_fragrance${enable_fragrance}.apk"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
        languageVersion = "1.9"
        apiVersion = "1.9"
    }
}

dependencies {
    api project(':tuotuotie')
    api project(':fragrance')
    implementation project(':baselibrary')
    api project(':ipcommon')

    // 根据CAR_PLATFORM_TYPE动态选择依赖
    def currentVehicleType = rootProject.ext.has("currentVehicleType") ? rootProject.ext.currentVehicleType : "none"

    // 根据CURRENT_VEHICLE_TYPE动态选择依赖
    switch (currentVehicleType) {
        case rootProject.ext.tinnove_8678_d4:
            // 518 车型配置
            implementation project(':iptinnove8678')
            implementation project(':iptinnovecommon')
            implementation name: 'android.car8678', ext: 'jar'
            implementation name: 'offline_C518-1.0.0.d_release', ext: 'jar'
            implementation name: 'virtualcar-platform-1.0.7.i', ext: 'aar'
            break
        case rootProject.ext.tinnove_8155_d3:
            implementation project(':iptinnove8678')
            implementation project(':iptinnovecommon')
            implementation name: 'carlib-new-1.0.2.918-175cb6c', ext: 'aar'
            implementation name: 'controller-lib-release_v3.1_20250417', ext: 'aar'
            implementation name: 'MLog-1.0.1.13-78dbef5', ext: 'aar'
            implementation name: 'bigsur-car-lib-release', ext: 'aar'
            implementation name: 'nexus-static-1.236.3.0-4e910b2', ext: 'jar'
            break
        case rootProject.ext.mega_8155_d3:
            implementation project(':ipmegacommon')
            implementation project(':ipdomestic8155')
            implementation name: 'carlib-new-1.0.2.918-175cb6c', ext: 'aar'
            implementation name: 'controller-lib-release_v3.1_20250417', ext: 'aar'
            implementation name: 'MLog-1.0.1.13-78dbef5', ext: 'aar'
            implementation name: 'bigsur-car-lib-release', ext: 'aar'
            implementation name: 'nexus-static-1.236.3.0-4e910b2', ext: 'jar'
            break
        case rootProject.ext.mega_8155_g3:
            implementation project(':ipmegacommon')
            implementation project(':ipglobal8155')
            implementation name: 'carlib-new-1.0.2.918-175cb6c', ext: 'aar'
            implementation name: 'controller-lib-release_v3.1_20250417', ext: 'aar'
            implementation name: 'MLog-1.0.1.13-78dbef5', ext: 'aar'
            implementation name: 'bigsur-car-lib-release', ext: 'aar'
            implementation name: 'nexus-static-1.236.3.0-4e910b2', ext: 'jar'
            break
        case rootProject.ext.mega_8295_d4:
            implementation project(':ipmegacommon')
            implementation project(':ipdomestic8295')
            implementation name: 'carlib-new-1.0.2.918-175cb6c', ext: 'aar'
            implementation name: 'controller-lib-release_v3.1_20250417', ext: 'aar'
            implementation name: 'MLog-1.0.1.13-78dbef5', ext: 'aar'
            implementation name: 'bigsur-car-lib-release', ext: 'aar'
            implementation name: 'nexus-static-1.236.3.0-4e910b2', ext: 'jar'
            break
        default:
            break
    }
    implementation 'com.google.android.material:material:1.9.0+'
    implementation name: 'smartlifemiddleware-release', ext: 'aar'
    implementation 'skin.support:skin-support:4.0.5'                   // skin-support
    implementation 'skin.support:skin-support-appcompat:4.0.5'         // skin-support 基础控件支持
    implementation 'skin.support:skin-support-design:4.0.5'            // skin-support-design material design 控件支持[可选]
    implementation 'skin.support:skin-support-constraint-layout:4.0.5' // skin-support-constraint-layout ConstraintLayout 控件支持[可选]
    implementation  "com.airbnb.android:lottie:6.1.0"
    implementation 'com.google.code.gson:gson:2.8.6'

    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    // Hilt
    implementation libs.hilt.android
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.3.0-rc01'
    kapt libs.hilt.compiler

    // Timber logging
    implementation libs.timber

    implementation("androidx.startup:startup-runtime:1.2.0")
}